%-----------------------------------------------------------------------------
% This is ETHIONUM.MF in text format as of February, 1993 by Abass B. Alamnehe.
%-----------------------------------------------------------------------------

% Program name:---------------- 100.mf
cmchar "The letter 100";
beginchar(oct "320",10u#,.5[cap_height#,x_height#],paren_depth#);
adjust_fit(-u#,u#);

% THE RIGHT TOP ARC
pickup fine.nib; penpos0(stem,180); penpos1(stem,180); penpos2(stem,180);
penpos2'(stem,180); pos3(vair,105); penpos0'(stem,180);

x0=.5w; x0'=.5w+.5u; x1=x0; x2r=w-6.5u;  x2'r=x2r+.25u; x3=w-3u;
bot y0'=.1h; y0=.2h; y1=.3h; y2=.6h; y2'=.4h; top y3=.8h;
filldraw stroke z3e{left}...{down}z2e..z2'e...z1e..z0e{down}..{right}z0'e;

penpos4(curve,0); pos5(vair,270);
x5=x2l; x4r=w-.5u; y5=.45h;  y4=.65h;
filldraw stroke z5e{right}..{up}z4e{up}..{left}z3e;

x9=x10=.65w; y9=y10=h+.075h;  
lft_wing(9,1.5u,.01h); rt_wing(10,1.25u,.01h);
x11=x12=.55w; bot y11=bot y12=-o;  
lft_wing(11,1.5u,.01h); rt_wing(12,1.5u,.01h);
penlabels(0,0',1,2,3,4,5); endchar;


% Program neme:---------------- 90.mf
cmchar "The letter 90";
beginchar(oct"317",10u#,.5[cap_height#,x_height#],paren_depth#);
adjust_fit(.5u#,0);

pickup fine.nib; penpos0(stem,5); penpos1(stem,0); penpos2(stem,0);
penpos3(stem,0); penpos4(stem,0); penpos5(stem,0); penpos6(stem,0);
pos7(vair,45);

x1=.5w; x0=x1+.5u; x2r=w-1.25u; x3=x0+1.5u;
x4l=w-5u; x5l=x4l-.25u; x6l=w-4.5u; x7=.5w;
bot y0=.1h; y1=.2h; y2=.55h; y3+o=y4=.55h; y5=y4+.025h; y6=.7h; top y7=.8h;

filldraw stroke z0e{left}...{up}z1e---z3e;
filldraw stroke z4e{left}...{up}z5e---z6e{up}...{left}z7e;
x8=x7; y8=y7; lft_wing(8,1.5u,o);

x9=x10=.5w; y9=y10=h+.075h;  
lft_wing(9,1.5u,.01h); rt_wing(10,1.25u,.01h);
x11=x12=.45w; bot y11=bot y12=-o;  
lft_wing(11,1.5u,.01h); rt_wing(12,1.5u,.01h);
penlabels(0,1,2,3,4,5,6,7); endchar;


% Program name:---------------- 80.mf
cmchar "The letter 80";
beginchar(oct "316",10u#,.55[cap_height#,x_height#],0);
adjust_fit(3u#,2.5u#)
if .5w <> good.x .5w :change_width; fi pickup fine.nib;


%LEFT STEM
penpos0(stem,0); penpos1(stem,0); penpos1'(stem,0); 
x0=w-7u; x1=x0-.5u;  x1'=x1-.5u;
y0=.75h-o; y1=.15h; y1'=.15(y0-y1)+y1; 
filldraw stroke z0e--z1'e{down}..{right}z1e;

%RIGHT STEM
penpos3(stem,0); penpos3'(stem,0); penpos2(stem,0); 
x2=w-x0; x3=x2-.5u; x3'=x3-.5u;
y2=.75h; y3=.15h; y3'=.15(y2-y3)+y3; 
filldraw stroke z2e--z3'e{down}..{right}z3e;

x4=x0r; x4'=x2l; y4=y0; y4'=y2; draw z4--z4';
x5=x0l; x6=x2r; y5=y0; y6=y2;
lft_wing(5,2u,.015h); rt_wing(6,1.5u,.015h);

x9=x10=.55w; y9=y10=h+.075h;  
lft_wing(9,1.75u,.01h); rt_wing(10,1.5u,.01h);
x11=x12=.5w; bot y11=bot y12=-o;  
lft_wing(11,1.75u,.01h); rt_wing(12,1.5u,.01h);
penlabels(0,1,2,3,4,5); endchar;



% Program name:---------------- 70.mf
cmchar "The letter 70";
beginchar(oct "315",10u#,.5[cap_height#,x_height#],paren_depth#);
adjust_fit(0,2u#);

pickup fine.nib; pos0(vair,90);  pos0'(vair,90); pos1(stem,180);
penpos2(stem,180); pos2'(stem,200); pos3(vair,270);

x0=w-3.5u; x0'=x0-1.5u; x2r=u; x1r=1.75u; x2'r=x2r+.25h; x3=w-4u;
top y0=.8h; top y0'=y0; y1=.6h; y2=.275h;  y2'=.15h-oo;  y3=.175h+o;
filldraw stroke z0e...z0'e{left}...z1e..z2e{down}...z2'e...z3e;

x7=x3; y7=y3; rt_half_ellipse(7,0);
x8=x0; y8=y0-.225h; large_ring(8,5);

x9=x10=.55w; y9=y10=h+.075h;  
lft_wing(9,1.5u,.01h); rt_wing(10,1.5u,.01h);
x11=x12=.5w; bot y11=bot y12=-o;  
lft_wing(11,1.5u,.01h); rt_wing(12,1.5u,.01h);
penlabels(0,1,2,2',3); endchar;


% Program neme:---------------- 60.mf
cmchar "The letter 60";
beginchar(oct "314",10u#,.5[cap_height#,x_height#],0);
adjust_fit(1.5u#,1.5u#);
if .5w <> good.x .5w :change_width; fi pickup fine.nib;

pickup fine.nib; penpos1(stem,0); penpos2(stem,0); penpos1'(stem,0);
penpos2'(stem,0); 
x1=w-5u; x1'=x1-.5u;  x2=x1'+u;  x2=x2'-.5u; 
top y1=.8h; y2=.3h; y1'=y2'+o=y2+.6(y1-y2); 
filldraw stroke z1e--z1'e; filldraw stroke z2'e--z2e;

x3=x1l; x4=x1r; y3=y4=y1; 
lft_wing(3,2u,.015h);  rt_wing(4,2.5u,.015h);
x5=x2l; x6=x2r; y5=y6=y2;  
lft_wing(5,2.25u,.015h);  rt_wing(6,1.5u,.010h);

x9=x10=.55w; y9=y10=h+.075h;  
lft_wing(9,1.5u,.01h); rt_wing(10,1.5u,.01h);
x11=x12=.5w; bot y11=bot y12=-o;  
lft_wing(11,1.5u,.01h); rt_wing(12,1.5u,.01h);
penlabels(1,2,3,4,6,7); endchar;


 % Program name:---------------- 50.mf
cmchar "The letter 50";
beginchar(oct "313",8.5u#,.55[cap_height#,x_height#],paren_depth#);
adjust_fit(u#,.5u#);

pickup fine.nib; penpos0(stem,180); penpos1(stem,180); penpos2(stem,180); 
penpos3(stem,180); pos4(vair,270); 
penpos8(stem,0); penpos8'(stem,0); penpos9(stem,0); penpos9'(stem,0);

x0r=hround max(.7u,1.45u-.5curve); x3r=x0r+hair; x1r=x0r+hair; x2r=x0r-stem;
x4l=.5w+fine; x8r=w-x0r-stem; x8'r=x8r+.5u; x9r=x8r-.5u; x9'r=x9r-.25u;

top y0=top y8=.75h+o; y3=.45h; y1=.75(y0-y3)+y3; y2=.25(y0-y3)+y3;
y4=y7=y3+o; bot y9=.1h; y8'=y9+.85(y8-y9);  y9'=y9+.1(y8-y9);
filldraw stroke z0e..controls z1e and z2e..z3e{right}...z4e;
filldraw stroke z8e{right}..{down}z8'e--z9'e{down}..{right}z9e;

x19=x20=.45w; y19=y20=h+.075h;  
lft_wing(19,1.25u,.01h); rt_wing(20,1.25u,.01h);
x21=x12=.45w; bot y21=bot y12=-o;  
lft_wing(21,1.25u,.01h); rt_wing(12,1.25u,.01h);
penlabels(1,2,3,4,5,6,7,8,9,10,11); endchar;


 % Program name:---------------- 40.mf
cmchar "The letter 40";
beginchar(oct "312",13.5u#,.55[cap_height#,x_height#],paren_depth#);
adjust_fit(serif_fit#+stem_shift#,-1.25u#);

numeric light_vair; light_vair=vair;  if hefty:-vround 2vair_corr fi;
if light_vair < fine.breadth:light_vair :=fine.breadth; fi

pickup fine.nib; penpos0(stem,180); penpos1(stem,180); penpos2(stem,180); 
penpos3(stem,180); pos4(vair,270); penpos5(stem,0); penpos6(stem,0); 
penpos7(stem,0); penpos8'(stem,0); penpos8(stem,0); penpos9(stem,0); 
pos10(vair,270); penpos9'(stem,0); pos11(stem,180);

x0r=hround max(.7u,1.45u-.5curve)+u; x3r=x0r+hair; x1r=x0r+hair; x2r=x0r-stem;
x5r=.525w; x6r=hround(x5r+fine); x7r=x4l=hround(x5r-.25stem);
x8r=w-x0r-u; x8'r=x8r+.25u; x9r=x8'r-u; x9'r=x9r-.25u;
x10r=.75[x5r,x8r]; x11r=x4r-u;

top y0=top y5=top y8=.775h; y3=.45h; y1=.75(y0-y3)+y3; y6=.75(y0-y3)+y3;
y2=.25(y0-y3)+y3; y4=y7=y3+o; bot y9=.15h; y8'=y9+.85(y8-y9); 
y9'=y9+.15(y8-y9); y10=y4; y11=y4+.1h;

filldraw stroke pulled_arc.e(11,10); filldraw stroke z5e..controls z6e..z7e;
filldraw stroke z0e..controls z1e and z2e..z3e{right}...z4e;
filldraw stroke z8e{right}..{down}z8'e--z9'e{down}..{right}z9e;

x19=x20=.5w; y19=y20=h+.075h;  
lft_wing(19,1.5u,.01h); rt_wing(20,1.5u,.01h);
x21=x12=.425w; bot y21=bot y12=-o;  
lft_wing(21,1.5u,.01h); rt_wing(12,1.5u,.01h);
penlabels(1,2,3,4,5,6,7,8,9,10,11); endchar;


% Program name:---------------- 30.mf
cmchar "The letter 30";
beginchar(oct "311",12u#,.5[cap_height#,x_height#],paren_depth#);
adjust_fit(.5u#,0);
numeric light_vair; light_vair=vair; if hefty:-vround 2vair_corr fi;
if light_vair < fine.breadth:light_vair:=fine.breadth; fi

pickup fine.nib;
penpos0(stem,0);  penpos0'(stem,0); penpos1(stem,0); penpos1'(stem,0);
pos2(vair,90);  penpos3(stem,180); penpos4(stem,180);
penpos4'(stem,0); pos5(vair,270); penpos6(stem,180);
penpos7(stem,180); pos8(vair,135);

x0r=x1r=w-1.25u; x0'r=x0r-.5stem; x1'=x1r; x2=(x0l-1.25u); x3l=x4l=(x2-1.25u);
x4'r=x4l; x5=(x4r-1.25u); x6l=(x5-1.25u);  x7l=.5[x5,x6l]; x8l=x5;

y0=.13h+oo; y1=.65h; y0'=.25(y1-y0)+y0; y1'=.75(y1-y0)+y0; y2=.75h;
y3=y1'; y4=y4'=y0'+.15h; y5=y0'-.1h; y6=y4; y7=y1'; y8=y2;

filldraw stroke z0e..controls z0'e and z1'e..super_arc.e(1,2)..
super_arc.e(2,3)..z3e{down}...z4e;
filldraw stroke super_arc.e(8,6) & super_arc.e(6,5) & super_arc.e(5,4');

x9=x10=.575w; y9=y10=h+.075h;  
lft_wing(9,1.5u,.01h); rt_wing(10,1.5u,.01h);
x11=x12=.5w; bot y11=bot y12=-o;  
lft_wing(11,1.5u,.01h); rt_wing(12,1.5u,.01h);
penlabels(1,2,3,4,5,6,7,8); endchar;


% Program name:---------------- 20.mf
cmchar "The letter 20";
beginchar(oct "310",10u#,.5[cap_height#,x_height#],paren_depth#);
adjust_fit(u#,u#); pickup fine.nib;

penpos0(stem,5); penpos0'(stem,0); penpos1(stem,0); penpos1'(stem,0);
x0=x0'=.5w;  x1=x0-2u;  x1'=x1-.5u;  y0=.775h; y1=.15h; y0'=y1+.8(y0-y1);
y1'=y1+.15(y0-y1);  
filldraw stroke z0e{down}..z0'e..z1'e{down}..{right}z1e; 

pos2(vair,90); penpos2'(stem,0);  penpos3(stem,0); penpos3'(stem,0);
x2=.5[x0,x1']; x2'=x2+3.5u; x3'=x2'+.5u; x3=x3'+.25u; 
y2=.5[y0,y1']; y2'=y2-.05h; y3'=y1';  y3=y1;
filldraw stroke z2e{right}...z2'e...{down}z3'e..z3e;

x4=x0l; y4=y0l;  lft_wing(4,2u,.015h); 
x5=x0r; y5=y0r;  rt_wing(5,2u,.015h);

x9=x10=.525w; y9=y10=h+.075h;  
lft_wing(9,1.5u,.01h); rt_wing(10,1.5u,.01h);
x11=x12=.5w; bot y11=bot y12=-o;  
lft_wing(11,1.5u,.01h); rt_wing(12,1.5u,.01h);
penlabels(1,2,3,4,5,6,7,8); endchar;


% Program name:---------------- 10.mf
cmchar "The letter 10";
beginchar("0",10u#,.55[cap_height#,x_height#],paren_depth#);
adjust_fit(0,0);

pickup fine.nib; penpos0(stem,5); penpos1(stem,0); 
penpos1'(stem,0); penpos0'(stem,0);
x1=w-5u; x1'=x1-.5u; x0=w-x1; x0'=x0+.5u;
top y0=.825h; bot y1=.15h; y0'=y1+.85(y0-y1); y1'=y1+.15(y0-y1);
filldraw stroke z0e{right}..{down}z0'e--z1'e{down}..{right}z1e;

% BOTTOM AND TOP WINGS
x9=x10=.55w; y9=y10=h+.075h;  
lft_wing(9,2u,.015h); rt_wing(10,1.5u,.015h);
x11=x12=.55w; bot y11=bot y12=-o;  
lft_wing(11,2u,.015h); rt_wing(12,1.5u,.015h);
labels(range 0 thru 13); endchar;


% Program name:---------------- 9.mf
cmchar "The letter 9";
beginchar("9",10u#,.5[cap_height#,x_height#],paren_depth#);
adjust_fit(u#,0)

% THE RIGHT STEM AND ARC
pickup fine.nib;  penpos0(stem,200); penpos1(stem,180);   
penpos2(stem,180); pos3(vair,270);
pos4(vair,270); penpos5(stem,0); penpos6(stem,0); penpos7(stem,15);

x0r=1.5u; x1r=2u; x2r=1.35u; x3r=.4w; x4=.5w; x5l=w-3.75u; x6l=w-3.25u; x7l=.6w;
y0=.7h; y1=.6h; y2=.3h; bot y3=.1h; bot y4=.125h; y5=.4h; y6=.675h; top y7=.8h;
filldraw stroke z0e...{down}z1e--z2e...z3e..z4e..z5e...z6e...z7e;

pos1'(vair,90); pos5'(vair,90); x1'=x1; y1'=y1-.15h; x5'=x6; y5'=y1'+.1h;
draw z1'--z5';

x9=x10=.5w; y9=y10=h+.075h;  
lft_wing(9,1.5u,.015h); rt_wing(10,1.5u,.015h);
x11=x12=.45w; bot y11=bot y12=00;  
lft_wing(11,1.5u,.015h); rt_wing(12,1.5u,.015h);
penlabels(0,1,2,3,4,5,6,7);  endchar;


 % Program name:---------------- 8.mf
cmchar "The letter 8";
beginchar("8",10u#,.5[cap_height#,x_height#],paren_depth#);
adjust_fit(2u#,u#);

pickup fine.nib; penpos0(stem,0); penpos1(stem,0); 
x0=.5w; x1=x0-.5u; y0=.775h;  y1=.225h; filldraw stroke z0e--z1e;

x3=x0l; x4=x0r; y3=y4=y0;  
lft_wing(3,1.75u,.015h); rt_wing(4,1.75u,.015h);

x5=x1l; x6=x1r; y5=y6=y1;  
lft_wing(5,1.75u,.015h); rt_wing(6,1.75u,.015h);

% UPPER AND LOWER RING
x9=x10=.5w; y9=y10=h+.075h;  
lft_wing(9,1.25u,.005h); rt_wing(10,1.25u,.005h);
x11=x12=.45w;  y11=y12=-.05h;  
lft_wing(11,1.25u,.005h); rt_wing(12,1.25u,.005h);
penlabels(1,2,3,4,5,6,7,8,9,10,11,12); endchar;


% Program name:---------------- 7.mf
cmchar "The letter 7";
beginchar("7",10u#,.55[cap_height#,x_height#],paren_depth#);
adjust_fit(u#,u#);

pickup fine.nib; penpos0(stem,5); penpos1(stem,0); 
penpos1'(stem,0); penpos0'(stem,0);
x1=w-5.5u; x1'=x1-.5u; x0=w-x1+.5u; x0'=x0+.5u;
top y0=.8h; bot y1=.15h; y0'=y1+.85(y0-y1); y1'=y1+.15(y0-y1);
filldraw stroke z0e{right}..{down}z0'e--z1'e{down}..{right}z1e;
x3=x0l; x2=x1;  y3=y0; y2=y1;
lft_wing(3,1.75u,.025h); rt_half_ellipse(2,2.5u);

% BOTTOM AND TOP WINGS
x9=x10=.575w; y9=y10=h+.075h;  
lft_wing(9,2u,.015h); rt_wing(10,1.5u,.015h);
x11=x12=.5w; bot y11=bot y12=-o;  
lft_wing(11,2u,.015h); rt_wing(12,1.5u,.015h);
labels(range 0 thru 13); endchar;


% Program name:---------------- 6.mf
cmchar "The letter 6";
beginchar("6",10u#,.55[cap_height#,x_height#],paren_depth#);
adjust_fit(0,0);

pickup fine.nib; penpos0(stem,5); penpos1(stem,0); 
penpos1'(stem,0); penpos0'(stem,0);
x1=w-5.5u; x1'=x1-.5u; x0=w-x1+.5u; x0'=x0+.5u;
top y0=.75h; bot y1=.1h; y0'=y1+.85(y0-y1); y1'=y1+.15(y0-y1);
filldraw stroke z0e{right}..{down}z0'e--z1'e{down}..{right}z1e;
x3=x0l; x2=x1;  y3=y0; y2=y1;
lft_half_ellipse(3,1.75u); half_ring(2);

% BOTTOM AND TOP WINGS
x9=x10=.55w; y9=y10=h+.075h;  
lft_wing(9,2u,.015h); rt_wing(10,1.5u,.015h);
x11=x12=.55w; bot y11=bot y12=-o;  
lft_wing(11,2u,.015h); rt_wing(12,1.5u,.015h);
labels(range 0 thru 13); endchar;


% Program name:---------------- 5.mf
cmchar "The letter 5";
beginchar("5",10u#,.5[cap_height#,x_height#],paren_depth#);
adjust_fit(0,u#);

pickup fine.nib; penpos0(stem,180);  penpos1(stem,180); penpos2(stem,180);
pos3(bar,270);   pos4(bar,360);
lft x0=x1=hround(w-5u);  x2l=x0-2.5u; x3l=hround(w-6.5u); x4=w-4u;
y0=.8h+o;  y1=.7h+o;  y2=.3h+o; y3=.15h+o; y4=.2h+o;
filldraw stroke z0e{down}...z1e...z2e{down}...z3e---z4e;

% RIGHT WING
x6=x1; y6=y1-.1h+o; rt_wing(6,2u,0);

% RIGHT LOWER WING
x7=x4; y7=y4; rt_half_ellipse(7,0);

%
x9=x10=.55w; y9=y10=h+.075h;  
lft_wing(9,2u,.015h); rt_wing(10,1.5u,.015h);
x11=x12=.55w; bot y11=bot y12=00;  
lft_wing(11,2u,.015h); rt_wing(12,1.5u,.015h);
penlabels(1,2,3,4,5,6); endchar;


% Program name:---------------- 4.mf
cmchar "The letter 4";
beginchar("4",9u#,.5[cap_height#,x_height#],paren_depth#);
adjust_fit(u#,u#);

pickup fine.nib; pos1(vair,90); pos3(vair,-90);
penpos2(stem,180); penpos4(stem,0);
x2r=u; x4r=w-x2r; x1=x3=.5w;
y1r=.8h; y3r=.2h; y2l=y4l=.5[y1r,y3r];
filldraw stroke pulled_arc.e(1,2)..pulled_arc.e(2,3)..
 pulled_arc.e(3,4)..pulled_arc.e(4,1);  % bowl

x9=x10=.55w; y9=y10=h+.075h;  
lft_wing(9,2u,.015h); rt_wing(10,1.5u,.015h);
x11=x12=.55w; bot y11=bot y12=00;  
lft_wing(11,2u,.015h); rt_wing(12,1.5u,.015h);
penlabels(1,2,3,4,5,6,7,8,9,10,11,12); endchar;


 % Program name:---------------- 3.mf
cmchar "The letter 3";
beginchar("3",9u#,.5[cap_height#,x_height#],paren_depth#);
adjust_fit(1u#,1u#);

pickup fine.nib; penpos0(stem,0); penpos1(stem,0); penpos1'(stem,0); 
x0=x1=w-7u; x1'=x0-.5u;
y0=.725h;  y1=.125h; y1'=y1+.1h; filldraw stroke z0e--z1'e{down}..{right}z1e;

% RIGHT PEAK
pickup fine.nib; x3=w-6u-fine; y3=y0; rt_wing(3,3.25u,.015h);

x9=x10=.55w; y9=y10=h+.075h;  
lft_wing(9,2u,.015h); rt_wing(10,1.5u,.015h);
x11=x12=.55w; bot y11=bot y12=00;  
lft_wing(11,2u,.015h); rt_wing(12,1.5u,.015h);
penlabels(1,2,3,4,5,6,7,8,9,10,11,12); endchar;


% Program name:---------------- 2.mf
cmchar "The letter 2";
beginchar("2",10u#,.5[cap_height#,x_height#],paren_depth#);
adjust_fit(-1u#,1.5u#);
numeric x_bar;

% THE RIGHT TOP ARC
pickup fine.nib; pos0(vair,270); pos1(1.5vair,180); penpos2(stem,180);
penpos2'(stem,180); pos3(vair,105);

x1=w-4.5u; x2r=w-7u;  x2'r=x2r+.2u; x3=w-3.5u; x0=w-2.5u;
y1=.15h; y0=y1-o; y2=.6h; y2'=.45h; top y3=.85h;
filldraw stroke z3e{left}...{down}z2e..z2'e...z1e;

penpos4(stem,0); pos5(vair,270);
x5=x2l; x4r=w-.75u;  y5=.55h;  y4=.7h;
filldraw stroke z5e{right}..{up}z4e{up}..{left}z3e;

x6=x1-.5u; y6=y1-oo; half_ring(6);

x9=x10=.625w; y9=y10=h+.075h;  
lft_wing(9,2u,.015h); rt_wing(10,1.75u,.015h);
x11=x12=.625w; bot y11=bot y12=00;  
lft_wing(11,2u,.015h); rt_wing(12,1.75u,.015h);
penlabels(0,0',1,2,2',3,4,5,6,7); endchar;


% Program name:---------------- 1.mf
cmchar "The letter 1";
beginchar("1",10u#,.5[cap_height#,x_height#],paren_depth#);
adjust_fit(.5u#,u#);

pickup fine.nib; penpos0(stem,0);  penpos0'(stem,0); penpos1(stem,0); 
penpos2(stem,0); pos3(stem,0);
lft x0=lft x1=w-5u; x0'=x0+.5u; x2l=w-8.5u; x3l=w-7u;
y0=.8h; y0'=y0-.05h;   y1=.65h;    y2=.25h; bot y3=.1h;
filldraw stroke z0e{right}..{down}z0'e{down}...z1e...z2e{down}...z3e{right};

pickup fine.nib; x4=w-7u;  x5=w-5u; y4=y5=.13h; draw flex(z4,z5);

pickup fine.nib; pos6(vair,135); penpos7(stem,0); pos8(vair,-135);
x6=x1-1u-fine; x7=x1+2u;  x8=x5; y6=.55h+o;  y8=y4; y7=.5[y8,y6];
filldraw stroke super_arc.e(6,7) & super_arc.e(7,8);

x9=x10=.575w; y9=y10=h+.05h;  
lft_wing(9,2u,.015h); rt_wing(10,2u,.015h);
x11=x12=.5w; bot y11=bot y12=00;  
lft_wing(11,2u,.015h); rt_wing(12,2u,.015h);
penlabels(1,2,3,4,5,6,7,8); endchar;
