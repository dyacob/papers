%-----------------------------------------------------------------------------
% This is ETHIOYN.MF in text format  as of February, 1993 by Abass B. Alamnehe.
%-----------------------------------------------------------------------------

% Program neme:---------------- yn1.mf
cmchar "The letter yn1";
beginchar(oct"157",7u#,.5[cap_height#,x_height#],paren_depth#);
adjust_fit(3.5u#,1.5u#);

pickup fine.nib; penpos0(stem,5); penpos1(stem,0); penpos2(stem,0);
penpos3(stem,5); penpos4(stem,5); penpos5(stem,0); penpos6(stem,0);
penpos7(stem,7);
x1l=w-5u; x0l=x1l+.5u; x2r=w-1.2u; x3r=x2r-.5u;
x4r=x3l; x5r=x4r-.25u; x6r=x4r+.25u; x7r=x4r-.25u;
bot y0=0; y1=.075h; y2=.575h; y3=y4=.625h; y5=.675h; y6=.875h; y7=.95h;
filldraw stroke z0e{left}...{up}z1e---z2e{up}...{left}z3e;
filldraw stroke z4e{left}...{up}z5e---z6e{up}...{left}z7e;

x8=x7l; x9=x7r; y8=y9=y7; lft_half_ellipse(8,1.25u);
rt_half_ellipse(9,1.75u);
penlabels(0,1,2,3,4,5,6,7); endchar;


% Program neme:---------------- yn2.mf
cmchar "The letter yn2";
beginchar(oct"332",7u#,.5[cap_height#,x_height#],paren_depth#);
adjust_fit(3.5u#,2.5u#);

pickup fine.nib; penpos0(stem,5); penpos1(stem,0); penpos2(stem,0);
penpos3(stem,5); penpos4(stem,5); penpos5(stem,0); penpos6(stem,0);
penpos7(stem,7);
x1l=w-5u; x0l=x1l+.5u; x2r=w-1.2u; x3r=x2r-.5u;
x4r=x3l; x5r=x4r-.25u; x6r=x4r+.25u; x7r=x4r-.25u;
bot y0=0; y1=.075h; y2=.575h; y3=y4=.625h; y5=.675h; y6=.875h; y7=.95h;
filldraw stroke z0e{left}...{up}z1e---z2e{up}...{left}z3e;
filldraw stroke z4e{left}...{up}z5e---z6e{up}...{left}z7e;

x8=x7l; x9=x7r; y8=y9=y7; lft_half_ellipse(8,1.25u);
rt_half_ellipse(9,1.75u);
x10=w-1.5u; y10=.45h; rt_wing(10,1.25u,0);
penlabels(0,1,2,3,4,5,6,7); endchar;


% Program neme:---------------- yn3.mf
cmchar "The letter yn3";
beginchar(oct"333",7u#,.5[cap_height#,x_height#],paren_depth#);
adjust_fit(3.5u#,2u#);

pickup fine.nib; penpos0(stem,5); penpos1(stem,0); penpos2(stem,0);
penpos3(stem,5); penpos4(stem,5); penpos5(stem,0); penpos6(stem,0);
penpos7(stem,7);
x1l=w-5u; x0l=x1l+.5u; x2r=w-1.2u; x3r=x2r-.5u;
x4r=x3l; x5r=x4r-.25u; x6r=x4r+.25u; x7r=x4r-.25u;
bot y0=0; y1=.075h; y2=.575h; y3=y4=.625h; y5=.675h; y6=.875h; y7=.95h;

filldraw stroke z0e{left}...{up}z1e---z2e{up}...{left}z3e;
filldraw stroke z4e{left}...{up}z5e---z6e{up}...{left}z7e;
x8=x0r-.5u; bot y8=o; rt_half_ellipse(8,1.5u);
x9=x7l; x10=x7r; y9=y10=y7; lft_half_ellipse(9,1.25u);
rt_half_ellipse(10,1.75u);
penlabels(0,1,2,3,4,5,6); endchar;


% Program neme:---------------- yn4.mf
cmchar "The letter yn4";
beginchar(oct"334",12u#,.5[cap_height#,x_height#],paren_depth#);
adjust_fit(.5u#,-.5u#);

pickup fine.nib; pos0(vair,90); pos1(vair,105); penpos2(stem,180);
penpos3(stem,180); pos4(vair,270); penpos5(stem,187); penpos6(stem,180);
penpos7(stem,180); penpos8(stem,180);
x0=w-4.5u; x1=.45w; x2r=1.75u; x3r=2.5u; x4=w-6u;
x5=x4; x6=x5+.5u; x7=x5; x8=x5+.5u;
y0=.8h; y1=.8h+.025h; y2=.55h; y3=.425h; y4=.45h; y5=y4; y6=y4-.1h; 
y7=.075h; bot y8=0;

filldraw stroke z0e{left}...z1e{left}...{down}z2e...z3e{right}..z4e;
filldraw stroke z5e{right}...{down}z6e---z7e{down}...{right}z8e;
x9=x0; y9=y0+o; rt_wing(9,0,-o);
penpos10(stem,5); penpos11(stem,5);
x10=x4-.5u; x11=x10+.25u; y10=y0+o; y11=y0+.2h; filldraw  stroke z10e--z11e;
x12=x11l; x13=x11r; y12=y13=y11; lft_half_ellipse(12,1.5u);
rt_half_ellipse(13,1.25u);
penlabels(0,1,2,3,4,5,6,7,8,10,11); endchar;


% Program neme:---------------- yn5.mf
cmchar "The letter yn5";
beginchar(oct"335",7u#,.5[cap_height#,x_height#],paren_depth#);
adjust_fit(3u#,1.5u#);

pickup fine.nib; penpos0(stem,0); penpos1(stem,0); penpos2(stem,0);
penpos3(stem,5); penpos4(stem,5); penpos5(stem,0); penpos6(stem,0);
penpos7(stem,7);
x1l=w-5u; x0l=x1l+.5u; x2r=w-1.2u; x3r=x2r-.5u;
x4r=x3l; x5r=x4r-.25u; x6r=x4r+.25u; x7r=x4r-.25u;
bot y0=0; y1=.075h; y2=.575h; y3=y4=.625h; y5=.7h; y6=.9h; y7=.975h;
filldraw stroke z0e{left}...{up}z1e---z2e{up}...{left}z3e;
filldraw stroke z4e{left}...{up}z5e---z6e{up}...{left}z7e;
x8=x0; bot y8=0; half_ring(8);
x9=x7l; x10=x7r; y9=y10=y7; lft_half_ellipse(9,1.25u);
rt_half_ellipse(10,1.75u);
penlabels(0,1,2,3,4,5,6); endchar;


def upper_left_wing(suffix @)=
      pickup fine.nib;  pos@g(vair,90); pos@i(vair,90);
      x@g=x@; x@i=x@-1.5u; y@g=y@i=y@; filldraw stroke z@g.e--z@i.e;
      x@a=x@-1.52u; y@a=y@;
      x@b=x@-3.9u;  x@c=x@-3.7u;  x@d=x@-4.2u;  x@e=x@-2.5u;   x@f=x@-2.5u;
      y@b=y@-oo; y@c=y@+.07h; y@d=y@+.15h; y@e=y@+.14h;  y@f=y@-oo;
      filldraw  flex(z@b,z@c,z@d) & flex(z@d,z@e,z@a) & flex(z@a,z@f,z@b) & cycle;
      labels(x@,@a,@b,@c,@d,@e,@f,@g,@i);
enddef;

def upper_right_wing(suffix $)=
      pickup fine.nib; pos$a(vair,90); pos$b(vair,90);
      x$a=x$; x$b=x$+1.5u; y$a=y$b=y$; filldraw stroke z$a.e--z$b.e;
      x$g=x$+1.52u; y$g=y$;
      x$i=x$+3.9u;  x$j=x$+3.7u;  x$k=x$+4.2u;  x$l=x$+2.5u;   x$m=x$+2.5u;
      y$i=y$-oo; y$j=y$+.07h; y$k=y$+.15h; y$l=y$+.14h;  y$m=y$-oo;
      filldraw flex(z$i,z$j,z$k) & flex(z$k,z$l,z$g) &  flex(z$g,z$m,z$i) & cycle;
      labels(x$,$g,$i,$j,$k,$l,$m,$a,$b);
enddef;


% Program neme:----------------yn6.mf
cmchar "The letter yn6";
beginchar(oct"336",10u#,.5[cap_height#,x_height#],paren_depth#);
adjust_fit(1u#,1u#);

pickup fine.nib; penpos0(stem,5); penpos1(stem,0); penpos2(stem,0);
penpos3(stem,0); penpos4(stem,0); penpos5(stem,0); penpos6(stem,0);
pos7(vair,45);
x1=.5w; x0l=x1l+.25u; x2r=w-1.25u; x3r=x2r-.5u;
x4l=w-5u; x5l=x4l-.25u; x6l=w-4.5u; x7=.5w;
bot y0=0; y1=.075h; y2=.5h; y3=.55h; y4=y3; y5=y4+.025h; y6=.75h; top y7=.85h;
filldraw stroke z0e{left}...{up}z1e---z2e{up}...{left}z3e;
filldraw stroke z4e{left}...{up}z5e---z6e{up}...{left}z7e;

x8=x7; y8=y7; lft_wing(8,1.5u,o);
penpos9(stem,0); penpos10(stem,0); x9+.25u=x10=x7; y9=y7; y10=y9+.125h;
filldraw stroke z9e--z10e;
x14=x9l+.25u; x15=x9r; y14=y15=y10;
upper_left_wing(14); upper_right_wing(15);
penlabels(0,1,2,3,4,5,6,7); endchar;

def ynseven_ring(suffix $)(expr theta,s)=
   pickup fine.nib;  transform t; t=identity slanted (s) 
    rotatedaround((.5w,.5h),theta);
   x$:=x$+.4u; y$:=y$-.125h;
   x$a=x$c=x$+1.75u; x$b=x$b'=x$; x$d=x$d'=x$a+2u; x$e=x$f=x$+1.75u;
   y$b=y$d'=y$a=y$+.195h; y$b'=y$d=y$c=y$-.05h; y$e=y$+.15h; y$f=y$-.025h;
   path p, q; p=z$a...z$b..z$b'..z$c..z$d..z$d'...cycle;  q=z$e...z$f...cycle;
   fill p transformed t;   pickup crisp.nib; unfill q transformed t;
   pickup fine.nib;
   penlabels(z$a,z$b,z$b',z$c,z$d,z$d',z$e,z$f);
enddef;

% Program neme:---------------- yn7.mf
cmchar "The letter yn7";
beginchar(oct"337",12u#,.5[cap_height#,x_height#],paren_depth#);
adjust_fit(.5u#,.5u#);

pickup fine.nib; pos0(vair,90); pos1(vair,105); penpos2(stem,180);
penpos3(stem,180); pos4(vair,270); penpos5(stem,185); penpos6(stem,180);
penpos7(stem,180); penpos8(stem,180);
x0=w-4.5u; x1=.45w; x2r=1.75u; x3r=2.5u; x4=w-6u;
x5=x4; x6=x5+.5u; x7=x5; x8=x5+.5u;
y0=.8h; y1=.8h+.025h; y2=.55h; y3=.425h; y4=.45h; y5=y4; y6=y4-.1h; 
y7=.075h; bot y8=0;
filldraw stroke z0e{left}...z1e{left}...{down}z2e...z3e{right}..z4e;
filldraw stroke z5e{right}...{down}z6e---z7e{down}...{right}z8e;
x9=x0-1.75u; y9=y0-.05h; ynseven_ring(9,0,1/6);

penpos10(stem,0); penpos11(stem,7);
x10=x4-.5u; x11=x10+.25u; y10=y0+o; y11=y0+.2h; filldraw  stroke z10e--z11e;

x12=x11l; x13=x11r; y12=y13=y11; lft_half_ellipse(12,1.5u);
rt_half_ellipse(13,1.25u);
penlabels(0,1,2,3,4,5,6,7,8,10,11); endchar;
