%-----------------------------------------------------------------------------
% This is ETHIOPP.MF in text format  as of February, 1993 by Abass B. Alamnehe.
%-----------------------------------------------------------------------------

% Program neme:---------------- pp1.mf
cmchar "The letter pp1";
beginchar(oct "160",10u#,.5[cap_height#,x_height#],0);
adjust_fit(1.5u#,1.5u#);
if .5w <> good.x .5w :change_width; fi pickup fine.nib;

% STEM
penpos1(stem,5); penpos2(1.2stem,5); penpos1'(stem,0); penpos2'(stem,0);
x1=w-5u; x2=x1; x1'=x1; x2'=x2-.5u;
top y1=h; bot y2=0; y1'=.75h; y2'=.15h;
filldraw stroke z1e---z2'e{down}...{right}z2e;

% WINGS
x6=x1-.5stem; x7=x1+.5stem; y7=y1; y6=y1-oo;
lft_wing(6,2.5u,.02h); rt_wing(7,2.5u,.02h);
penlabels(1,2,3,4,6,7); endchar;


% Program neme:---------------- pp2.mf
cmchar "The letter pp2";
beginchar(oct "340",10u#,.5[cap_height#,x_height#],0);
adjust_fit(1.5u#,1.5u#);
if .5w <> good.x .5w :change_width; fi pickup fine.nib;

% STEM
penpos1(stem,5); penpos2(1.2stem,5); penpos1'(stem,0); penpos2'(stem,0);
x1=w-5u; x2=x1; x1'=x1; x2'=x2-.5u;
top y1=h; bot y2=0; y1'=.75h; y2'=.15h;
filldraw stroke z1e---z2'e{down}...{right}z2e;

% WINGS
x5=x1r; y5=.4h; rt_half_ellipse(5,1.5u);
x6=x1-.5stem; x7=x1+.5stem; y7=y1; y6=y1-oo;
lft_wing(6,2.5u,.02h); rt_wing(7,2.5u,.02h);
penlabels(1,2,3,4,5,6,7); endchar;


% Program neme:----------------pp3.mf
cmchar "The letter pp3";
beginchar(oct "341",10u#,.5[cap_height#,x_height#],0);
adjust_fit(1.5u#,1.5u#);

if .5w <> good.x .5w :change_width; fi pickup fine.nib;
% STEM
penpos1(stem,5); penpos2(1.2stem,5); penpos1'(stem,0); penpos2'(stem,0);
x1=w-5u; x2=x1; x1'=x1; x2'=x2-.5u;
top y1=h; bot y2=0; y1'=.75h; y2'=.15h;
filldraw stroke z1e---z2'e{down}...{right}z2e;

% WINGS
penpos5(stem,0); x5l=x2; bot y5=00; rt_half_ellipse(5,1.5u);
x6=x1-.5stem; x7=x1+.5stem; y7=y1; y6=y1-oo;
lft_wing(6,2.5u,.02h); rt_wing(7,2.5u,.02h);
penlabels(1,2,3,4,5); endchar;


def t_wing(suffix $)=
      pickup fine.nib;  x$g=x$; y$g=y$;
      x$i=x$-1.9u;  x$j=x$-1.7u;  x$k=x$-2.2u;  x$l=x$-.5u;   x$m=x$-.5u;
      y$i=y$; y$j=y$-.07h; y$k=y$-.15h; y$l=y$-.14h;  y$m=y$;
      filldraw flex(z$i,z$j,z$k) & flex(z$k,z$l,z$g) &  flex(z$g,z$m,z$i) & cycle;
      pickup fine.nib;
      labels(x$,$g,$i,$j,$k,$l,$m,$a,$b);
enddef;

% Program neme:---------------- pp4.mf
cmchar "The letter pp4";
beginchar(oct "342",10u#,.55[cap_height#,x_height#],0);
adjust_fit(2.5u#,1.5u#);
if .5w <> good.x .5w :change_width; fi pickup fine.nib;

% STEM
penpos1(stem,5); penpos2(stem,0); penpos1'(stem,0); pos3(vair,-90);
x1=x2=w-5u; x1'=x2;  x3=x4=0;
top y1=h; bot y2=.45h; y1'=.75h; y3=y4=.1h;
filldraw stroke z1e..controls z1'e..z2e..pulled_arc.e(2,3); t_wing(4);

% WINGS
x7=x1-.5stem; x8=x1+.5stem; y7=y1-oo; y8=y1;
lft_wing(7,2.5u,.02h); rt_wing(8,2.5u,.02h);
penlabels(1,2,3,4,5,6); endchar;


% Program neme:---------------- pp5.mf
cmchar "The letter pp5";
beginchar(oct "343",10u#,.5[cap_height#,x_height#],0);
adjust_fit(1.5u#,1.5u#);
if .5w <> good.x .5w :change_width; fi pickup fine.nib;

% STEM
penpos1(stem,5); penpos2(1.2stem,5); penpos1'(stem,0); penpos2'(stem,0);
x1=w-5u; x2=x1; x1'=x1; x2'=x2-.5u;
top y1=h; bot y2=0; y1'=.75h; y2'=.15h;
filldraw stroke z1e---z2'e{down}...{right}z2e;

% WINGS
penpos5(stem,0); x5=x2-fine; bot y5=00; half_ring(5);
x6=x1-.5stem; x7=x1+.5stem; y7=y1; y6=y1-oo;
lft_wing(6,2.5u,.02h); rt_wing(7,2.5u,.02h);
penlabels(1,2,3,4,5); endchar;


% Program neme:---------------- pp6.mf
cmchar "The letter pp6";
beginchar(oct "344",10u#,.5[cap_height#,x_height#],0);
adjust_fit(1.5u#,1.5u#);
if .5w <> good.x .5w :change_width; fi pickup fine.nib;

% STEM
penpos1(stem,5); penpos2(stem,5); penpos1'(stem,0); penpos2'(stem,0);
penpos3(stem,0);
x1=w-4.5u; x2=w-5u; x1'=x1-.5u; x2'=x1'+u; x3=x2-.25u;
top y1=h; bot y2=0; y1'=y2'=.7h; y3=.1h;
filldraw stroke z1e--z1'e; filldraw stroke z2'e---z3e{down}..{right}z2e;

% WINGS
x6=x1-.5stem; x7=x1+.5stem; y7=y1; y6=y1-oo;
lft_wing(6,2.5u,.02h); rt_wing(7,2.5u,.02h);
penlabels(1,2,3,4,6,7); endchar;


% Program neme:---------------- pp7.mf
cmchar "The letter pp7";
beginchar(oct "345",11u#,.5[cap_height#,x_height#],0);
adjust_fit(2u#,u#);
if .5w <> good.x .5w :change_width; fi pickup fine.nib;

% STEM
penpos1(stem,5); penpos2(1.2stem,5); penpos1'(stem,-15); penpos2'(stem,-15);
x1=w-5u; x1'=x1; x2'=0; x2=x2';
top y1=h; bot y2=0; y1'=.65h; y2'=.15h;
filldraw stroke z1e...z1'e..z2'e..z2e{right};

% WINGS
x6=x1-.5stem; x7=x1+.5stem; y7=y1; y6=y1-oo;
lft_wing(6,2.5u,.02h); rt_wing(7,2.5u,.02h);
penlabels(1,2,3,4,6,7); endchar;


