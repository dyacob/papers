%-------------------------------------------------------------
% This is ETH_MRF.MF      as of Apr, 1996 by Jochen Metzinger
% originally distributed by Abass B. Alamnehe [February, 1993]
%-------------------------------------------------------------

iff known eth_mya:
cmchar "The letter ~mA";
beginchar(eth_mya,16u#,.5[cap_height#,x_height#],0);
adjust_fit(-.2u#,1u#);
if .5w <> good.x .5w :change_width; fi pickup fine.nib;

penpos0(vair,90); penpos1(vair,90); penpos0'(vair,90); penpos2(stem,-180); 
penpos3(stem,180); penpos4(vair,200); penpos5(vair,270); penpos5'(stem,-0);
penpos6(stem,-0); penpos6'(stem,0); penpos7(stem,180); penpos11(stem,180);
penpos8(stem,180); penpos8'(vair,270); penpos9(stem,0);
penpos9'(stem,0); penpos10(stem,180);
penpos12(vair,270);  penpos13(stem,0); penpos13'(stem,0);

lft x2r=hround u; x9'l=hround(w); x9r=x9'l-.5stem; x0'=x9;
x5'=x5l=w-.625w; x1=x5l+stem;
lft x4r=hround(x2r+1.25stem); x6=x5l+stem; x12l=x8r=w-.375w; x7r=x8r-.5u;
x3=.5[x6,x2r]; x11r=hround(x7r-stem); x8'r=x9r-u;
x10r=hround(x7r+stem); x13=w-3u; x13'=x13-stem;

top y0=top y0'=0.85h+o; y9=0.85h+oo; top y1=.825h+o;  y2=y1-.25h; bot y4=.4h; 
 y3=.5[y1,y2]; y5'=y4+oo; y6=.5[y0,y1]-oo;
y7=.5[y0,y1]; y12=y8=y4+.05h-oo; y11=y8+.1h; y8'=y8+oo;
y10=y7-.1h; bot y13=-0.05h; y13'=.25(y9-y13)+y13; y9'=.8(y9-y13)+y13;

filldraw stroke z6e--z5'e...{left}z4e..super_arc.e(2,1)---z0'e;
filldraw stroke z8'e{left}...z12e;
filldraw stroke z9e..controls z9'e and z13'e ..z13e;
pickup fine.nib; filldraw stroke z7e..controls z10e and z11e..z8e{right};

penpos14(stem,0); penpos15(stem,5);
x14r=x15r=x7r; y14=y7; y15=y14+0.175h;
filldraw stroke z14e--z15e;

x16=x15l+.25u; x17=x15r; y16=y17=y15-oo;
upper_left_wing(16); upper_right_wing(17);
penlabels(0,1,2,3,4,5,5',6,7,8,8',9,10,11,12,14,15); endchar;


iff known eth_rya:
cmchar "The letter ~ri";
beginchar(eth_rya,10u#,.5[cap_height#,x_height#],paren_depth#);
adjust_fit(0,1u#);

pickup fine.nib; penpos0(stem,180);  penpos1(stem,180); penpos2(stem,180);
pos3(vair,270); pos4(vair,270);   penpos5(stem,0); penpos6(stem,10);
x0l=x1l=w-3u;  x2r=u; x3=w-5.5u; x4=w-4.5u; x5r=w-u; x6r=x5r-.5u;
top y0=.95h;  y1=.85h;  y2=.2h-o; bot y3=.025h; y4=.065h;  y5=.2h; y6=.3h;
filldraw stroke z0e{down}...z1e...z2e{down}...z3e---z4e..z5e{up}...z6e;

x14=x0l; x15=x0r; y14=y15=y0;
rt_half_ellipse(14,1.5u); lft_half_ellipse(15,1.5u);
penlabels(0,1,2,3,4,5,6); endchar;


iff known eth_fya:
cmchar "The letter ~fi";
beginchar(eth_fya,10u#,.5[cap_height#,x_height#],paren_depth#);
adjust_fit(0,3u#);

pickup fine.nib; penpos0(stem,180);  penpos1(stem,180); 
penpos2(1.1stem,180); penpos3(stem,195); pos4(vair,270); pos5(vair,270); 
pos6(vair,270); penpos6'(1.2stem,360);
x0l=x1l=w-3u; x2r=.5u; x3r=x2r+u; x4=w-2.5u; x5=w-1.5u;
top y0=.95h; y1=.85h; y2=.15h; bot y3=0; y4=.1h; y5=.1h;
filldraw stroke z0e...z1e...z2e{down}...z3e...z4e---z5e;
x6=x5-.5u; x6'=x6+2.5u; y6=y5; y6'=y6+.25h; filldraw stroke pulled_arc.e(6,6');

penpos7(stem,25); penpos8(stem,0); pos9(vair,90);
x7=x4-1.5u; x8=x7+.5u; x9=x2l; y7=y4-o; y9=.3h; y8=y7+.4(y9-y7);
filldraw stroke z7e..pulled_arc.e(8,9) shifted(.25u,-.025h) rotated 15;

x14=x0l; x15=x0r; y14=y15=y0;
rt_half_ellipse(14,1.5u); lft_half_ellipse(15,1.5u);
penlabels(0,1,2,3,4,5,6); endchar;








