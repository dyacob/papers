%-------------------------------------------------------------
% This is ETH_PU.MF     as of Apr, 1996 by Jochen Metzinger
% originally distributed by Abass B. Alamnehe [February, 1993]
%-------------------------------------------------------------

iff known eth_pua:
cmchar "The letter pua";
beginchar(eth_pua,10u#,.5[cap_height#,x_height#],0);
adjust_fit(1.5u#,3.5u#);
if .5w <> good.x .5w :change_width; fi pickup fine.nib;

% STEM
penpos1(stem,5); penpos2(1.2stem,5); penpos2'(stem,0);
x1=w-5u; x2=x1; x2'=x2-.5u;
top y1=h; bot y2=0; y2'=.15h;
filldraw stroke z1e---z2'e{down}...{right}z2e;

% WINGS
x6=x1-.5stem; x7=x1+.5stem; y7=y1; y6=y1-oo;
lft_wing(6,2.5u,.02h); rt_wing(7,2.5u,.02h);
penlabels(1,2,3,4,6,7); 

pos20(vair,90); pos20'(vair,90); 
x20=x2'r; x20'=x20+2.5u; y20=y20'=.4h; draw z20--z20'; 
x21=x20'; y21=y20'-.245h; large_ring(21,7);
penlabels(20);
endchar;


iff known eth_puI:
cmchar "The letter puI";
beginchar(eth_puI,10u#,.5[cap_height#,x_height#],0);
adjust_fit(1.5u#,2.5u#);
if .5w <> good.x .5w :change_width; fi pickup fine.nib;

% STEM
penpos1(stem,5); penpos2(1.2stem,5); penpos2'(stem,0);
x1=w-5u; x2=x1; x2'=x2-.5u;
top y1=h; bot y2=0; y2'=.15h;
filldraw stroke z1e---z2'e{down}...{right}z2e;

% WINGS
x6=x1-.5stem; x7=x1+.5stem; y7=y1; y6=y1-oo;
lft_wing(6,2.5u,.02h); rt_wing(7,2.5u,.02h);
penlabels(1,2,3,4,6,7); 

pos20(vair,90); pos20'(vair,90); 
x20=x2'r; x20'=x20+1.5u;  y20=y20'=.4h;
filldraw stroke z20--z20';

penpos21(stem,0); penpos21'(stem,0); penpos22(stem,0);
x21l=x21'l=x20'r; x22l=x21l+.5u; y21=y20-.1h;
y21'=y20+.2h; y22=y21+.75(y21'-y21);
filldraw stroke z21e---z22e{up}..{left}z21'e;

x23=x21r; y23=y21;
rt_wing(23,1u,-.01h);
penlabels(20,20',21,21',22,23);
endchar;


iff known eth_puA:
cmchar "The letter puA";
beginchar(eth_puA,10u#,.55[cap_height#,x_height#],0);
adjust_fit(5.0u#,1.5u#);
if .5w <> good.x .5w :change_width; fi pickup fine.nib;

% STEM
penpos1(stem,5); penpos2(stem,0);
penpos3(stem,0); penpos4(stem,0);
x1=x2+.3u=w-5u; x3l=x4l+.5u=0;
top y1=h; bot y2=.45h; y3=.15h; y4=0;
filldraw stroke z1e--z2e & pulled_arc.e(2,3)..z4e;

% WINGS
x7=x1-.5stem; x8=x1+.5stem; y7=y1-oo; y8=y1;
lft_wing(7,2.5u,.02h); rt_wing(8,2.5u,.02h);
penlabels(1,2,3,4,5,6); 

x20=x4l; x21=x4r; y20=y21=y4; 
lft_wing(20,1.75u,.015h); rt_wing(21,1.5u,.015h);
endchar;


iff known eth_puX:
cmchar "The variant letter puA";
beginchar(eth_puX,10u#,.5[cap_height#,x_height#],0);
adjust_fit(1.5u#,1.5u#);
if .5w <> good.x .5w :change_width; fi pickup fine.nib;

% STEM
penpos1(stem,5); penpos2(1.2stem,5); penpos1'(stem,0); penpos2'(stem,0);
x1=w-5u; x2=x1; x1'=x1; x2'=x2-.5u;
top y1=h; bot y2=0; y1'=.75h; y2'=.15h;
filldraw stroke z1e---z2'e{down}...{right}z2e;

% WINGS
x6=x1-.5stem; x7=x1+.5stem; y7=y1; y6=y1-oo;
lft_wing(6,2.5u,.02h); rt_wing(7,2.5u,.02h);
penlabels(1,2,3,4,6,7); 

z20=z2l; lft_wing(20,1.75u,.015h); 
z21=z2r; rt_wing(21,1.5u,.015h);
endchar;


iff known eth_puE:
cmchar "The letter puE";
beginchar(eth_puE,10u#,.5[cap_height#,x_height#],0);
adjust_fit(1.5u#,1.5u#);
if .5w <> good.x .5w :change_width; fi pickup fine.nib;

% STEM
penpos1(stem,5); penpos2(1.2stem,5); penpos1'(stem,0); penpos2'(stem,0);
x1=w-5u; x2=x1; x1'=x1; x2'=x2-.5u;
top y1=h; bot y2=0; y1'=.75h; y2'=.15h;
filldraw stroke z1e---z2'e{down}...{right}z2e;

% WINGS
penpos5(stem,0); x5=x2-fine; bot y5=00; half_ring(5);
x6=x1-.5stem; x7=x1+.5stem; y7=y1; y6=y1-oo;
lft_wing(6,2.5u,.02h); rt_wing(7,2.5u,.02h);
penlabels(1,2,3,4,5);

z20=z5l; lft_wing(20,2u,.045h);  
penlabels(20);
endchar;


iff known eth_pue:
cmchar "The letter pue";
beginchar(eth_pue,10u#,.5[cap_height#,x_height#],0);
adjust_fit(1.5u#,1.5u#);
if .5w <> good.x .5w :change_width; fi pickup fine.nib;

% STEM
penpos1(stem,5); penpos2(1.2stem,5); penpos2'(stem,0);
x1=w-5u; x2=x1; x2'=x2-.5u;
top y1=h; bot y2=0; y2'=.15h;
filldraw stroke z1e---z2'e{down}...{right}z2e;

% WINGS
x6=x1-.5stem; x7=x1+.5stem; y7=y1; y6=y1-oo;
lft_wing(6,2.5u,.02h); rt_wing(7,2.5u,.02h);
penlabels(1,2,3,4,6,7); 

pos20(vair,90); pos20'(vair,90); 
x20=x2'r; x20'=x20+1.5u;  y20=y20'=.4h;
filldraw stroke z20--z20';

penpos21(stem,0); penpos21'(stem,0); penpos22(stem,0);
x21l=x21'l=x20'r; x22l=x21l+.25u; y21=y20; 
y21'=y21+.25h; y22=y21+.75(y21'-y21);
filldraw stroke z21e---z22e{up}..{left}z21'e;

x23=x21r; y23=y21;
rt_wing(23,0,-.01h);
penlabels(20,20',21,21',22,23);
endchar;
