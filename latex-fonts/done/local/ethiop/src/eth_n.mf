%-------------------------------------------------------------
% This is ETH_N.MF      as of Dec, 1995 by Jochen Metzinger
% originally distributed by Abass B. Alamnehe [February, 1993]
%-------------------------------------------------------------

iff known eth_na:
cmchar "The letter na";
beginchar(eth_na,7u#,.5[cap_height#,x_height#],paren_depth#);
adjust_fit(0,0);

pickup fine.nib; penpos0(stem,5); penpos1(stem,0); penpos2(stem,0);
penpos3(vair,5); penpos4(stem,0); penpos5(stem,0); penpos6(stem,5);
x1l=w-5u; x0l=x1l+.5u; x2r=w-1.2u; x3l=w-4u; x4l=u; x5l=1.5u; x6l=u;
bot y0=0; y1=.075h; y2=.6h; y3=.65h; y4=.675h; y5=.925h; y6=.975h;
filldraw stroke z0e{left}...{up}z1e---z2e{up}...{left}z3e{left}
...{up}z4e---z5e{up}...{left}z6e;
penlabels(0,1,2,3,4,5,6); endchar;


iff known eth_nU:
cmchar "The letter nU";
beginchar(eth_nU,7u#,.5[cap_height#,x_height#],paren_depth#);
adjust_fit(0,3u#);

pickup fine.nib; penpos0(stem,5); penpos1(stem,0); penpos2(stem,0);
penpos3(stem,5); penpos4(stem,5); penpos5(stem,0); penpos6(stem,0);
penpos7(stem,5);
x1l=w-5u; x0l=x1l+.5u; x2r=w-1.2u; x3r=x2r-.5u;
x4r=x3l; x5r=x4r-.25u; x6r=x4r+.25u; x7r=x4r-.25u;
bot y0=0; y1=.075h; y2=.6h; y3=.65h; y4=.65h; y5=.7h; y6=.9h; y7=.975h;

filldraw stroke z0e{left}...{up}z1e---z2e{up}...{left}z3e;
filldraw stroke z4e{left}...{up}z5e---z6e{up}...{left}z7e;
x8=w-1.75u; y8=.45h; rt_wing(8,1.25u,0);
penlabels(0,1,2,3,4,5,6); endchar;


iff known eth_nI:
cmchar "The letter nI";
beginchar(eth_nI,7u#,.5[cap_height#,x_height#],paren_depth#);
adjust_fit(0,2u#);

pickup fine.nib; penpos0(stem,5); penpos1(stem,0); penpos2(stem,0);
penpos3(stem,5); penpos4(stem,5); penpos5(stem,0); penpos6(stem,0);
penpos7(stem,5);
x1l=w-5u; x0l=x1l+.5u; x2r=w-1.2u; x3r=x2r-.5u;
x4r=x3l; x5r=x4r-.25u; x6r=x4r+.25u; x7r=x4r-.25u;
bot y0=0; y1=.075h; y2=.6h; y3=.65h; y4=.65h; y5=.7h; y6=.9h; y7=.975h;

filldraw stroke z0e{left}...{up}z1e---z2e{up}...{left}z3e;
filldraw stroke z4e{left}...{up}z5e---z6e{up}...{left}z7e;
x8=x0r-.5u; bot y8=o; rt_half_ellipse(8,1.5u);
penlabels(0,1,2,3,4,5,6); endchar;


iff known eth_nA:
cmchar "The letter nA";
beginchar(eth_nA,12u#,.5[cap_height#,x_height#],paren_depth#);
adjust_fit(-.5u#,-2u#);

pickup fine.nib; pos0(vair,90); pos1(vair,105); penpos2(stem,180);
penpos3(stem,180); pos4(vair,270); penpos5(stem,187); penpos6(stem,180);
penpos7(stem,180); penpos8(stem,180);
x0=w-5u; x1=.45w; x2r=1.75u; x3r=2.5u; x4=w-6u;
x5=x4; x6=x5+.5u; x7=x5; x8=x5+.5u;
y0=.9h; y1=.9h-o; y2=.55h; y3=.425h; y4=.45h; y5=y4; 
y6=y4-.1h; y7=.075h; bot y8=0;

filldraw stroke z0e{left}...z1e{left}...{down}z2e...z3e{right}..z4e;
filldraw stroke z5e{right}...{down}z6e---z7e{down}...{right}z8e;
z9=z0; rt_wing(9,0,0);
penlabels(0,1,2,3,4,5,6,7,8); endchar;


iff known eth_nE:
cmchar "The letter nE";
beginchar(eth_nE,7u#,.5[cap_height#,x_height#],paren_depth#);
adjust_fit(-.25u#,1.5u#);

pickup fine.nib; penpos0(stem,0); penpos1(stem,0); penpos2(stem,0);
penpos3(stem,5); penpos4(stem,5); penpos5(stem,0); penpos6(stem,0);
penpos7(stem,5);
x1l=w-5u; x0l=x1l+.5u; x2r=w-1.2u; x3r=x2r-.5u;
x4r=x3l; x5r=x4r-.25u; x6r=x4r+.25u; x7r=x4r-.25u;
bot y0=0; y1=.075h; y2=.6h; y3=.65h; y4=.65h; y5=.7h; y6=.9h; y7=.975h;

filldraw stroke z0e{left}...{up}z1e---z2e{up}...{left}z3e;
filldraw stroke z4e{left}...{up}z5e---z6e{up}...{left}z7e;
x8=x0; bot y8=0; half_ring(8);
penlabels(0,1,2,3,4,5,6); endchar;


iff known eth_ne:
cmchar "The letter ne";
beginchar(eth_ne,10u#,.5[cap_height#,x_height#],paren_depth#);
adjust_fit(0,-.5u#);

pickup fine.nib; penpos0(stem,5); penpos1(stem,0); penpos2(stem,0);
penpos3(stem,0); penpos4(stem,0); penpos5(stem,0); penpos6(stem,0);
pos7(vair,45);
x1=.5w; x0l=x1l+.25u; x2r=w-1.25u; x3r=x2r-.5u;
x4l=w-5u; x5l=x4l-.25u; x6l=w-4.5u; x7=.5w;
bot y0=0; y1=.075h; y2=.55h; y3=.6h; y4=y3; y5=y4+.025h; y6=.85h; top y7=.95h;

filldraw stroke z0e{left}...{up}z1e---z2e{up}...{left}z3e---z4e;
filldraw stroke z4e{left}...{up}z5e---z6e{up}...{left}z7e;
x8=x7; y8=y7; lft_wing(8,1.5u,o);
penlabels(0,1,2,3,4,5,6,7); endchar;


iff known eth_nO:
cmchar "The letter nO";
beginchar(eth_nO,12u#,.5[cap_height#,x_height#],paren_depth#);
adjust_fit(-.5u#,-.5u#);

pickup fine.nib; pos0(vair,90); pos1(vair,105); penpos2(stem,180);
penpos3(stem,180); pos4(vair,270); penpos5(stem,185); penpos6(stem,180);
penpos7(stem,180); penpos8(stem,180);
x0=.55w; x1=x0-1u; x2r=1.75u; x3r=2.5u; x4=w-6u;
x5=x4; x6=x5+.5u; x7=x5; x8=x5+.5u;
y0=.9h; y1=.9h-o; y2=.55h; y3=.425h; y4=.45h; y5=y4; y6=y4-.1h; 
y7=.075h; bot y8=0;

filldraw stroke z0e{left}...z1e{left}...{down}z2e...z3e{right}..z4e;
filldraw stroke z5e{right}...{down}z6e---z7e{down}...{right}z8e;
x9=x0-1.75u; y9=y0-o; nfour_ring(9,0,1/6);
penlabels(0,1,2,3,4,5,6,7,8); endchar;


iff known eth_nuA:
cmchar "The letter nuA";
beginchar(eth_nuA,12u#,.5[cap_height#,x_height#],paren_depth#);
adjust_fit(-.5u#,0);

pickup fine.nib; pos0(vair,90); pos1(vair,105); penpos2(stem,180);
penpos3(stem,180); pos4(vair,270); penpos5(stem,187); penpos6(stem,180);
penpos7(stem,180); penpos8(stem,180);

x0=w-5u; x1=.45w; x2r=1.75u; x3r=2.5u; x4=w-6u;
x5=x4; x6=x5+.5u; x7=x5; x8=x5+.5u;
y0=.9h; y1=.9h-o; y2=.55h; y3=.425h; y4=.45h; y5=y4; 
y6=y4-.1h; y7=.075h; bot y8=0;

filldraw stroke z0e{left}...z1e{left}...{down}z2e...z3e{right}..z4e;
filldraw stroke z5e{right}...{down}z6e---z7e{down}...{right}z8e;
x9=x0; y9=y0+o; rt_wing(9,0,-o);

x20=x8r; x21=x8l; y20=y21=y8; lft_wing(20,2u,.015h); 
rt_wing(21,1.25u,.015h);
penlabels(0,1,2,3,4,5,6,7,8); endchar;
