%-------------------------------------------------------------
% This is ETH_F.MF      as of Apr, 1996 by Jochen Metzinger
% originally distributed by Abass B. Alamnehe [February, 1993]
%-------------------------------------------------------------

iff known eth_fa:
cmchar "The letter fa";
beginchar(eth_fa,10u#,.5[cap_height#,x_height#],paren_depth#);
adjust_fit(0,3u#);

pickup fine.nib; penpos0(1.2stem,187);  penpos1(1.1stem,180); 
penpos2(1.1stem,180); penpos3(stem,195);   pos4(vair,270); pos5(vair,270);
x0l=w-3u; x1l=x0l+.5u; x2r=.5u; x3r=x2r+u; x4=w-2.5u; x5=w-.5u;

top y0=h;  y1=.9h;  y2=.15h; bot y3=0; y4=.15h; y5=.15h;
filldraw stroke z0e{right}..{down}z1e...z2e{down}...z3e...z4e---z5e;
x6=x5-.5u; y6=y5; rt_half_ellipse(6,0);

penpos7(stem,25); penpos8(stem,0); pos9(vair,90);
x7=x4-1.5u; x8=x7+.5u; x9=x2l; y7=y4-o; y9=.3h; y8=y7+.4(y9-y7);
filldraw stroke z7e..pulled_arc.e(8,9) shifted(.25u,-.025h) rotated 15;
penlabels(0,1,2,3,4,5,6); endchar;


iff known eth_fU:
cmchar "The letter fU";
beginchar(eth_fU,10u#,.5[cap_height#,x_height#],paren_depth#);
adjust_fit(1u#,u#);

pickup fine.nib; penpos0(1.2stem,185);  penpos1(1.1stem,180); 
penpos2(1.1stem,180); penpos3(stem,195);   penpos5(1.2stem,0); penpos5'(stem,0); 
penpos6(stem,0); penpos6'(stem,0); pos3'(vair,270);

x0=w-6u; x1=x0+.5u; x2l=2u; x3=x2l+u; x3'=w-3u; x4=x3';
x7=x2; x6=x7+3.5u; x6'=x6+.5u;  x5=x7+2.5u;  x5'=x5-.5u;
top y0=h;  y1=.9h;  y2=.45h; y3=.35h; y3'=.4h; y4=.4h; 
y7=.6h-o; y6=.6h; y6'=y6-.1h; bot y5=0; y5'=y5+.075h;

filldraw stroke z0e{right}..{down}z1e...z2e{down}...z3e---z3'e;
rt_half_ellipse(4,0);  draw z7--z6;
filldraw stroke z6e{right}..{down}z6'e---z5'e{down}..{right}z5e;
penlabels(0,1,2,3,4,5,6); endchar;


iff known eth_fI:
cmchar "The letter fI";
beginchar(eth_fI,10u#,.5[cap_height#,x_height#],paren_depth#);
adjust_fit(0,3u#);

pickup fine.nib; penpos0(1.2stem,187);  penpos1(1.1stem,180); 
penpos2(1.1stem,180); penpos3(stem,195); pos4(vair,270); pos5(vair,270); 
pos6(vair,270); penpos6'(1.2stem,360);
x0l=w-3u; x1l=x0l+.5u; x2r=.5u; x3r=x2r+u; x4=w-2.5u; x5=w-1.5u;
top y0=h;  y1=.9h;  y2=.15h; bot y3=0; y4=.1h; y5=.1h;
filldraw stroke z0e{right}..{down}z1e...z2e{down}...z3e...z4e---z5e;
x6=x5-.5u; x6'=x6+2.5u; y6=y5; y6'=y6+.25h; filldraw stroke pulled_arc.e(6,6');

penpos7(stem,25); penpos8(stem,0); pos9(vair,90);
x7=x4-1.5u; x8=x7+.5u; x9=x2l; y7=y4-o; y9=.3h; y8=y7+.4(y9-y7);
filldraw stroke z7e..pulled_arc.e(8,9) shifted(.25u,-.025h) rotated 15;
penlabels(0,1,2,3,4,5,6); endchar;


iff known eth_fA:
cmchar "The letter fA";
beginchar(eth_fA,10u#,.5[cap_height#,x_height#],paren_depth#);
adjust_fit(1u#,u#);

pickup fine.nib; penpos0(1.2stem,185);  penpos1(1.1stem,180); 
penpos2(1.1stem,180); pos3(stem,195);   penpos5(1.2stem,0); penpos5'(stem,0); 
penpos6(stem,0); penpos6'(stem,0); pos3'(vair,270); 

x0=w-6u; x1=x0+.75u; x2l=2u; x3=x2l+u; x3'=w-4.5u; x4=x3';
x7=x2; x6=x7+3.75u; x6'=x6+.5u;  x5=x7+3.75u;  x5'=x5-.5u;
top y0=h;  y1=.9h;  y2=.45h; y3=.35h; y3'=.35h; y4=.35h; 
y7=.6h-o; y6=.6h; y6'=y6-.1h; bot y5=0; y5'=y5+.075h;

filldraw stroke z0e{right}..{down}z1e...z2e{down}...z3e---z3';
rt_wing(4,2u,-.015h);  draw z7..z6;
filldraw stroke z6e{right}..{down}z6'e---z5'e{down}..{right}z5e;
penlabels(0,1,2,3,4,5,6); endchar;


iff known eth_fE:
cmchar "The letter fE";
beginchar(eth_fE,10u#,.5[cap_height#,x_height#],paren_depth#);
adjust_fit(0,4.25u#);

pickup fine.nib; penpos0(1.2stem,187);  penpos1(1.1stem,180); 
penpos2(1.1stem,180); penpos3(stem,195); pos4(vair,270); pos5(vair,270); 
pos6(vair,270); penpos6'(1.2stem,360);
x0l=w-3u; x1l=x0l+.5u; x2r=.5u; x3r=x2r+u; x4=w-2.5u; x5=w-1u;
top y0=h;  y1=.9h;  y2=.15h; bot y3=0; y4=.1h; y5=.1h;
filldraw stroke z0e{right}..{down}z1e...z2e{down}...z3e...z4e---z5e;
x6=x5; y6=y5-.225h;  large_ring(6,5);

penpos7(stem,25); penpos8(stem,0); pos9(vair,90);
x7=x4-1.5u; x8=x7+.5u; x9=x2l; y7=y4-o; y9=.3h; y8=y7+.4(y9-y7);
filldraw stroke z7e..pulled_arc.e(8,9) shifted(.25u,-.025h) rotated 15;
penlabels(0,1,2,3,4,5,6); endchar;


iff known eth_fe:
cmchar "The letter fe";
beginchar(eth_fe,10u#,.5[cap_height#,x_height#],paren_depth#);
adjust_fit(0,1.5u#);

pickup fine.nib; pos0(vair,90);  pos0'(vair,90); pos1(1.2stem,180);
penpos2(stem,180); pos2'(stem,190); %% pos3(vair,270);
x0=w-2.5u; x0'=x0-2u; x2r=u; x1r=1.5u; x2'r=x2r+.25h; x3=w-3.5u;
top y0=.95h; top y0'=y0; y1=.75h; y2=.5h;  y2'=.4h;  y3=.4h;
filldraw stroke z0e...z0'e...z1e..z2e{down}...z2'e---z3;
x4=x3; y4=y3-o; rt_wing(4,2u,0); x4'=x0l; y4'=y0; r_wing(4');

penpos6(stem,0); penpos6'(stem,0); penpos5(stem,0); penpos5'(stem,0);
x7=x2; x6=x7+2.75u; x6'=x6+1.5u;  x5=x7+3.5u;  x5'=x5-.25u;
y7=.6h+.05h; y6=.6h; y6'=y6-.1h; bot y5=0; y5'=y5+.075h;
filldraw stroke z6e{right}..{down}z6'e---z5'e{down}..{right}z5e;
penlabels(0,0',1,2,2',3,4,5,5',6,6',7); endchar;


iff known eth_fO:
cmchar "The letter fO";
beginchar(eth_fO,10u#,.5[cap_height#,x_height#],paren_depth#);
adjust_fit(0,3u#);

pickup fine.nib; pos0(vair,90);  pos0'(vair,90); pos1(1.2stem,180);
penpos2(stem,180); pos2'(stem,190); pos3(vair,270); pos4(vair,270);
x0=w-3u; x0'=x0-1.75u; x2r=u; x1r=1.75u; x2'r=x2r+.25h; x3=w-3u; x4=x3+2u;
top y0=.95h; top y0'=y0; y1=.75h; y2=.15h;  bot y2'=0;  y3=y4=.125h;
filldraw stroke z0e...z0'e...z1e..z2e{down}..z2'e...z3e---z4e;
x5=x0; y5=y0-.2h; large_ring(5,2); 
x6=x4-.5u; y6=y4; rt_half_ellipse(6,0);

penpos7(stem,25); penpos8(stem,0); pos9(vair,100);
x7=x3-1.25u; x8=x7+.5u; x9=x2l; y7=y3-o; y9=.3h; y8=y7+.4(y9-y7);
filldraw stroke z7e..pulled_arc.e(8,9) shifted(.25u,-.025h) rotated 15;
penlabels(0,0',1,2,2',3,4,5,5',6,6',7); endchar;
